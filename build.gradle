apply plugin: 'c'
apply plugin: 'cpp'

/* dependencies */
task depLibuv << {
    def buildScript = 'deps/libuv/build.sh'
    if (System.properties['os.name'].toLowerCase().contains('windows')) {
        buildScript = 'deps/libuv/build.ps1'
    }
    def proc = new File(buildScript).getAbsolutePath().execute()
    println "${proc.text}"
}

task depGtest << {
    def buildScript = 'deps/gtest/build.sh'
    if (System.properties['os.name'].toLowerCase().contains('windows')) {
        buildScript = 'deps/gtest/build.ps1'
    }
    def proc = new File(buildScript).getAbsolutePath().execute()
    println "${proc.text}"
}

/* end dependencies */

model {
    buildTypes {
        debug
        release
    }

    repositories {
        libs (PrebuiltLibraries) {
            uv {
                headers.srcDir 'deps/libuv/include'
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile = file('deps/libuv/lib/libuv.a')
                }
            }
            gtest {
                headers.srcDir 'deps/gtest/include'
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile  = file('deps/gtest/lib/libgtest.a')
                }
            }
        }
    }
}

def gtestBuild = new File('deps/gtest/lib/libgtest.a')
def libuvBuild = new File('deps/libuv/lib/libuv.a')

binaries.all {
    lib library: 'uv', linkage: 'static'
    if (buildType == buildTypes.debug) {
        cCompiler.args '-g'
        cppCompiler.args '-g'
    }
}

libraries {
    editor {
        binaries.all {
            lib library: 'uv', linkage: 'static'
            if (!libuvBuild.exists()) {
                tasks.withType(CppCompile) {
                    dependsOn depLibuv
                }
            }
        }
    }
}

executables {
    test {
        binaries.all {
            lib library: 'gtest', linkage: 'static'
            if (!gtestBuild.exists()) {
                tasks.withType(CppCompile) {
                    dependsOn depGtest
                }
            }
        }
    }
}

task test << {
    def testName = 'test'
    def testDir = project.buildDir.getAbsolutePath() + '/binaries/testExecutable/'
    def releaseTest = new File(testDir + 'release/' + testName)
    def debugTest = new File(testDir + 'debug/' + testName)
    if (releaseTest.exists()) {
        def proc = releaseTest.getAbsolutePath().execute()
        println "Running Release Build Tests...\n${proc.text}"
    } else if (debugTest.exists()) {
        def proc = debugTest.getAbsolutePath().execute()
        println "Running Debug Build Tests...\n${proc.text}"
    } else {
        println 'You need to build a test target first!\nRun "gradle tasks" and look for build tasks with "test" in the name\n'
    }
}

task wrapper (type: Wrapper) {
    gradleVersion = '2.2'
}
